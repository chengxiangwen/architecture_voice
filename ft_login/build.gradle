apply plugin: 'com.android.application'
apply plugin: 'com.alibaba.arouter'

android {
    compileSdkVersion rootProject.android.compileSdkVersion
    buildToolsVersion rootProject.android.buildToolsVersion


    defaultConfig {
        applicationId 'com.josecheng.ft_login'
        minSdkVersion rootProject.android.minSdkVersion
        targetSdkVersion rootProject.android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        //ARouter配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName(), AROUTER_GENERATE_DOC: "enable"]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation rootProject.depsLibs.appcompat
    //eventbus类库依赖
    implementation rootProject.depsLibs.eventbus
    //gson解析库
    implementation rootProject.depsLibs.gson
    //arouter库
    implementation(rootProject.depsLibs.arouterapi) {
        exclude group: 'com.android.support'
    }
    annotationProcessor rootProject.depsLibs.aroutercompiler

    //网络请求库
    implementation project(':lib_network')
    //业务基础库
    implementation project(':lib_base')
    //基础UI库
    implementation project(':lib_common_ui')
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

//引用replugin-host-gradle插件
apply plugin: 'replugin-plugin-gradle'

repluginPluginConfig {
    pluginName = 'ft_login' //定义插件名
    hostApplicationId = 'com.josecheng.architecture_voice' //宿主applicationId
    hostAppLauncherActivity = 'com.josecheng.ft_loading.view.loading.LoadingActivity' //宿主工程启动activity
}

//添加依赖
dependencies {
    implementation 'com.qihoo360.replugin:replugin-plugin-lib:2.3.3'
}
